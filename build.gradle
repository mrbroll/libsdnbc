apply plugin: 'c'
apply plugin: 'cpp'

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.ajoberstar:gradle-git:0.12.0' }
}

import org.ajoberstar.grgit.*

def handleDep = { uri, depName, buildScript ->
    def tmpDir = new File(depName)
    tmpDir.mkdirs()
    Grgit.clone(dir: tmpDir, uri: uri)
    def proc = new File(buildScript).getAbsolutePath().execute()
    println "building ${depName}...\n${proc.text}"
}

task deps << {
    def gtestScript = 'deps/gtest/build.sh'
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        gtestScript = 'deps/gtest/build.bat'
    }
    handleDep ('https://github.com/mrbroll/gtest-mirror.git', 'gtest', gtestScript)
}

model {
    buildTypes {
        debug
        release
    }
    repositories {
        libs (PrebuiltLibraries) {
            gtest {
                headers.srcDir './deps/gtest/include'
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile  = file('./deps/gtest/lib/libgtest.a')
                }
            }
        }
    }
}

binaries.all {
    if (buildType == buildTypes.debug) {
        cCompiler.args '-g'
        cppCompiler.args '-g'
    }
}

libraries {
    sdnbc {}
}

def gtestBuildSDNBC = new File('./deps/gtest/lib/libgtest.a')

executables {
    test {
        binaries.all {
            lib libraries.sdnbc.static
            lib library: 'gtest', linkage: 'static'
            if (!gtestBuildSDNBC.exists()) {
                tasks.withType(CppCompile) { 
                    dependsOn deps
                }
            }
            tasks.withType(LinkExecutable) {
                doLast {
                    if (outputFile.exists()) {
                        println 'Run Tests with "gradle sdnbcTests"\n'
                    }
                }
            }
        }
    }
}

task test << {
    def testName = 'test'
    def binDir = project.buildDir.getAbsolutePath() + '/binaries/testExecutable/'
    def releaseTest = new File(binDir + 'release/' + testName)
    def debugTest = new File(binDir + 'debug/' + testName)
    if (releaseTest.exists()) {
        def proc = releaseTest.getAbsolutePath().execute()
        println "Running Release Build Tests...\n${proc.text}"
    } else if (debugTest.exists()) {
        def proc = debugTest.getAbsolutePath().execute()
        println "Running Debug Build Tests...\n${proc.text}"
    } else {
        println "You need to build a test executable first!\n"
    }
}

task wrapper (type: Wrapper) {
    gradleVersion = '2.2'
}
